buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7.1'
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = '1.7'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

ext {
    roboVMVersion = System.getProperty('version')
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { 
        credentials {
            username "$ppbuildsUsername"
            password "$ppbuildsPassword"
        }
        url 'http://ppbuilds.servebeer.com:8081/repository/internal/'
    }
}

dependencies {
    compileOnly gradleApi()
    compileOnly localGroovy()
    compile "org.apache.commons:commons-compress:1.5"
    compile "com.mobidevelop.robovm:robovm-dist-compiler:${roboVMVersion}"
    compile "com.mobidevelop.robovm:robovm-debugger:${roboVMVersion}"
    compile 'org.sonatype.aether:aether:1.13.1'
    compile 'org.sonatype.aether:aether-connector-wagon:1.13.1'
    compile 'org.apache.maven:maven-aether-provider:3.0.4'
    compile 'org.apache.maven.wagon:wagon-provider-api:2.4'
    compile 'org.apache.maven.wagon:wagon-http:2.4'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

shadowJar {
    classifier = ''
    dependencies {
        exclude(dependency("com.mobidevelop.robovm:robovm-dist-compiler:${roboVMVersion}"));
    }
    relocate 'org.apache.http', 'com.mobidevelop.robovm.org.apache.http'
    relocate 'org.apache.commons.io', 'com.mobidevelop.robovm.org.apache.commons.io'
    relocate 'org.objectweb.asm', 'com.mobidevelop.robovm.asm'
}

group = 'com.mobidevelop.robovm'
version = System.getProperty('version')

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username "$ppbuildsUsername"
                password "$ppbuildsPassword"
            }
            url 'http://ppbuilds.servebeer.com:8081/repository/internal'
        }
    }
}

assemble.dependsOn('shadowJar')